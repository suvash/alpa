alpa = _{SOI ~ program ~ EOI }

program = { expr* }

expr = { boolean | number | symbol | sexpr | qexpr }

boolean = { satya | galat } // true false are rust keywords
  satya = { "सत्य" }
  galat = { "गलत" }

anka = { "०" | "१" | "२" | "३" | "४" | "५" | "६" | "७" | "८" | "९" }

number = @{ ( "+" | "-" )? ~ sankhya }
  sankhya = { anka+ }

symbol = { exprs_op | numbers_op | qexpr_op | qexprs_op | sexpr_op | identifier }
  exprs_op = { equal | not_equal }
    equal  = { "==" } // बराबर
    not_equal  = { "!=" } // बराबर नभएको
  numbers_op = { add | subtract | multiply | divide | gte | gt | lte | lt }
    add      = { "+" } // "जोड"
    subtract = { "-" } // "घटाऊ"
    multiply = { "*" } // "गुणन"
    divide   = { "/" } // "भाग"
    gte      = { ">=" } // ठुलो अथवा बराबर
    gt       = { ">" } // ठुलो
    lte      = { "<=" } // सानो अथवा बराबर
    lt       = { "<" } // सानो
  qexpr_op = { first | rest | len | eval }
    first  = { "पहिलो" }
    rest   = { "बाँकी" }
    len    = { "वटा" }
    eval   = { "बिस्तार" }
  qexprs_op = { cons | join | def | put | lambda }
    cons    = { "निर्माण" }
    join    = { "एकत्र" }
    def     = { "नामक" }
    put     = { "=" }
    lambda  = { "\\" }
  sexpr_op   = { quote | printenv }
    quote    = { "उद्धरण" }
    printenv = { "वातावरण" }
  identifier = @{ (chinha | swar | byanjan | maatraa | halanta | purnabiram | chhotkari | avagraha | aum | anka | "_")+ }
    // See https://www.unicode.org/charts/PDF/U0900.pdf for details
    // only picking valid नेपाली (nepali) parts, not the entire devanagari chart
    // including underscore
    chinha     = { "ँ" | "ं" | "ः" }
    swar       = { "अ" | "आ" | "इ" | "ई" | "उ" | "ऊ" | "ए" | "ऐ" | "ओ" | "ओ" | "ऋ" }
    byanjan    = { "क" | "ख" | "ग" | "घ" | "ङ" | "च" | "छ" | "ज" | "झ" | "ञ" | "ट" | "ठ" | "ड" | "ढ" | "ण" | "त" | "थ" | "द" | "ध" | "न" | "प" | "फ" | "ब" | "भ" | "म" | "य" | "र" | "ल" | "व" | "श" | "ष" | "स" | "ह" }
    maatraa    = { "ा" | "ि" | "ी" | "ु" | "ू" | "ृ" | "े" | "ै" | "ो" | "ौ" }
    halanta    = { "्" }
    purnabiram = { "।" }
    chhotkari  = { "॰" }
    avagraha   = { "ऽ" }
    aum        = { "ॐ" }

sexpr = { "(" ~ expr* ~ ")"}

qexpr = { "'(" ~ expr* ~ ")"}

WHITESPACE = _{ " " | "\t" }
