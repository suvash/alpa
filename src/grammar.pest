alpa = _{SOI ~ program ~ EOI }

program = { expr* }

expr = { number | symbol | sexpr | qexpr }

sexpr = { "(" ~ expr* ~ ")"}

qexpr = { "'(" ~ expr* ~ ")"}

symbol = { number_op | qexpr_op | qexprs_op | sexpr_op }
  number_op = { add | subtract | multiply | divide }
    add      = { "+" } // "जोड"
    subtract = { "-" } // "घटाऊ"
    multiply = { "*" } // "गुणन"
    divide   = { "/" } // "भाग"
  qexpr_op = { first | rest | eval }
    first = { "पहिलो" }
    rest = { "बाँकी" }
    eval = { "बिस्तार" }
  qexprs_op = { join }
    join = { "एकत्र" }
  sexpr_op = { quote }
    quote = { "उद्धरण" }

number = @{ ( "+" | "-" )? ~ '०'..'९'+ }


WHITESPACE = _{ " " | "\t" }